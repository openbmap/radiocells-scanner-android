apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    compileSdkVersion 23
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "org.openbmap"
        minSdkVersion 22
        targetSdkVersion 23
    }


    sourceSets {
        main {
            file("${rootDir}/spatialite-android/natives").eachDir() { dir ->
                jniLibs.srcDirs += "${dir.path}/lib"
            }
        }
        androidTest {
            java.srcDirs = ['src/androidTest']
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    useLibrary 'org.apache.http.legacy'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // replace with the current version of the Android plugin
        classpath 'com.android.tools.build:gradle:2.2.0'
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'org.mapsforge:spatialite-android'
    compile 'cz.msebera.android:httpclient:4.4.1.1'
    compile 'com.android.support:support-v4:23.+'
    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.android.support:support-v13:23.+'
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.caverock:androidsvg:1.2.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.jjoe64:graphview:4.1.0'
    compile 'org.apache.httpcomponents:httpmime:4.2.3'
    compile 'org.mapsforge:mapsforge:0.6.1'
    compile 'org.mapsforge:mapsforge-core:0.6.1'
    compile 'org.mapsforge:mapsforge-map:0.6.1'
    compile 'org.mapsforge:mapsforge-poi-android:0.6.1'
    compile 'org.mapsforge:mapsforge-map-android:0.6.1'
    compile 'org.mapsforge:mapsforge-map-reader:0.6.1'
}

apt {
    arguments {
        eventBusIndex "org.openbmap.MyEventBusIndex"
    }
}

task writeVersionInfo {
    // This will output something like 'bf16f5d', or 'bf16f5d-dirty' if the tree has uncommitted changes
    def cmd = "git describe --match=null --always --dirty"
    def version = cmd.execute().text.trim()

    def resRawDir = new File(android.sourceSets.main.res.srcDirs[0], 'raw')
    resRawDir.mkdirs()
    def output = new File(resRawDir, 'build.txt')
    output.write(version)
}

gradle.projectsEvaluated {
    preBuild.dependsOn writeVersionInfo
}
